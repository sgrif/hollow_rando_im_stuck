id_start = _{ XID_START | "$" }
id_continue = _{ XID_CONTINUE | "'" | "-" }
ident = @{ id_start ~ id_continue* }
room_name = @{ id_start ~ (id_continue | " ")* }
transition = @{ ident ~ ("[" ~ room_name ~ "]")? }

int = @{ ASCII_DIGIT+ }
notch_cost = { "$NotchCost[" ~ int ~ ("," ~ int)* ~ "]" }
num = _{ int | notch_cost }

logic_operator = _{ and | or }
  and = { "+" }
  or = { "|" }

comparison_operator = { "<=" | ">=" | "<" | "=" | ">" }

logic_operation = _{ logic_term ~ (logic_operator ~ logic_term)* }
comparison_operation = _{ ident ~ comparison_operator ~ num }
paren_expression = { "(" ~ (logic_operation | comparison_operation) ~ ")"}

term = _{ num | logic_term }
logic_term = _{ comparison_operation | transition | ident | paren_expression }

logic = _{ SOI ~ logic_operation+ ~ EOI }

WHITESPACE = _{ " " | "\t" }
